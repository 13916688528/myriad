apply plugin: 'java'
apply plugin: 'application'

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

mainClassName = "com.ebay.myriad.Main"

group       = "com.ebay.myriad"
version     = "0.0.1"

configurations {
	provided
	capsule
}

repositories {
	mavenLocal()
	mavenCentral()
}

dependencies {
	ext {
		mesosVer = "0.20.0"
		dropwizardVer = "0.7.1"
	}
	capsule "co.paralleluniverse:capsule:0.7.1"
    compile "org.apache.mesos:mesos:${ext.mesosVer}"
    compile 'com.squareup.retrofit:retrofit:1.6.1'
    compile 'com.google.inject:guice:3.0'
    compile 'org.apache.commons:commons-collections4:4.0'
 	compile "io.dropwizard:dropwizard-core:${ext.dropwizardVer}"
 	compile "io.dropwizard:dropwizard-assets:${ext.dropwizardVer}"
 	compile "io.dropwizard:dropwizard-views-mustache:${ext.dropwizardVer}"
	compile "org.apache.curator:curator-framework:2.6.0"
	compile "com.lmax:disruptor:3.3.0"
	
    testCompile 'junit:junit:4.11'
}

run {
    systemProperty "myriad.config", "build/resources/main/config.yml"
}

task capsule(type: Jar, dependsOn: jar) {
    archiveName = "myriad-capsule-${version}.jar"

    from jar
    from { configurations.runtime }
	from { sourceSets.main.resources }
	
    from(configurations.capsule.collect { zipTree(it) }) { include 'Capsule.class' }

    manifest {
        attributes(
            'Main-Class'  		: 'Capsule',
            'Application-Class' : mainClassName,
            'Min-Java-Version' 	: '1.7.0',
            'JVM-Args' 			: run.jvmArgs.join(' '),
            'System-Properties' : (run.systemProperties + ["myriad.config": '$CAPSULE_DIR/config.yml']).collect { k,v -> "$k=$v" }.join(' ')
        )
    }
}
